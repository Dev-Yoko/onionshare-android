plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'org.jlleitschuh.gradle.ktlint' version '10.2.1'
}

static def versionCodeEpoch() {
    return (new Date().getTime() / 1000).toInteger()
}

def gitCommit = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short=7', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdk 33

    defaultConfig {
        applicationId "org.onionshare.android"
        minSdk 21
        targetSdk 32
        versionCode 10
        versionName "0.1.9-alpha"

        vectorDrawables {
            useSupportLibrary true
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments disableAnalytics: 'true'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        benchmark {
            signingConfig signingConfigs.debug
            debuggable false
        }
    }
    flavorDimensions "releaseType"
    productFlavors {
        stable {
            dimension "releaseType"
        }
        nightly {
            dimension "releaseType"
            applicationIdSuffix ".nightly"
            versionCode versionCodeEpoch()
            versionNameSuffix " ($gitCommit)"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += '-opt-in=kotlin.RequiresOptIn'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.3'
    }
    packagingOptions {
        exclude 'META-INF/*'
        // Due to https://github.com/Kotlin/kotlinx.coroutines/issues/2023
        exclude 'META-INF/licenses/*'
        exclude '**/attach_hotspot_windows.dll'
    }
    lintOptions {
        disable 'MissingTranslation'
    }
}

configurations {
    tor
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.activity:activity-compose:1.6.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'
    implementation 'com.google.android.material:material:1.8.0'

    implementation platform("androidx.compose:compose-bom:$compose_version")
    implementation 'androidx.compose.material:material'
    implementation "androidx.compose.material:material-icons-extended"
    implementation 'androidx.compose.ui:ui-tooling-preview'

    implementation "androidx.navigation:navigation-compose:2.5.3"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"
    implementation "androidx.profileinstaller:profileinstaller:1.2.2"
    implementation "androidx.security:security-crypto:1.1.0-alpha05"

    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    implementation 'org.briarproject:dont-kill-me-lib:0.2.5'
    implementation 'org.briarproject:moat-api:0.2'
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'

    implementation 'info.guardianproject:tor-android:0.4.7.8'
    implementation 'info.guardianproject:jtorctl:0.4.5.7'
    tor 'org.briarproject:obfs4proxy-android:0.0.14-tor2'
    tor 'org.briarproject:snowflake-android:2.5.1'

    def ktor_version = '2.2.3'
    implementation "io.ktor:ktor-server-core:$ktor_version"
    implementation "io.ktor:ktor-server-netty:$ktor_version"
    implementation "io.ktor:ktor-server-pebble:$ktor_version"
    implementation "io.ktor:ktor-server-status-pages:$ktor_version"
    implementation "io.ktor:ktor-server-call-logging:$ktor_version"
    implementation 'com.github.tony19:logback-android:2.0.1'

    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'

    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:1.3.3"
    androidTestImplementation 'tools.fastlane:screengrab:2.1.1'
}

def torLibsDir = 'src/main/jniLibs'

task cleanTorBinaries {
    outputs.dir torLibsDir
    doLast {
        delete fileTree(torLibsDir)
    }
}

clean.dependsOn cleanTorBinaries

task unpackTorBinaries {
    outputs.dir torLibsDir
    doLast {
        copy {
            from configurations.tor.collect { zipTree(it) }
            into torLibsDir
        }
    }
    dependsOn cleanTorBinaries
}
preBuild.dependsOn unpackTorBinaries

apply from: "${rootProject.rootDir}/gradle/ktlint.gradle"
